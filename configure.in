#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)

#--- Versions are set here

AC_INIT(managelogs, 2.2.1, managelogs-bugs@tekwire.net)

LIB_LT_VERSION="2"
AC_SUBST(LIB_LT_VERSION)

LIB_LT_REVISION="0"
AC_SUBST(LIB_LT_REVISION)

LIB_LT_AGE="0"
AC_SUBST(LIB_LT_AGE)

RELEASE_NUMBER="1"
AC_SUBST(RELEASE_NUMBER)

#------------------------


AC_CONFIG_SRCDIR([src/managelogs.c])
AM_CONFIG_HEADER(config.h)
AC_CONFIG_AUX_DIR(build)

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE
AM_AUX_DIR_EXPAND
AM_PROG_INSTALL_SH
AM_WITH_DMALLOC

# Checks for programs.

dnl Make sure we have an ANSI compiler
AM_C_PROTOTYPES
test "x$U" != "x" && AC_MSG_ERROR(Compiler not ANSI compliant)

dnl Disable C++ and Fortran checks in configure

m4_defun([_LT_AC_LANG_CXX_CONFIG], [:])
m4_defun([_LT_AC_LANG_F77_CONFIG], [:])

#AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

AC_PROG_INSTALL

# Checks for header files.

AC_HEADER_STDC

# Check this as it is not always set by APR

AC_CHECK_HEADERS([sys/stat.h])

# Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_SIZE_T

#----------------------------------------------------------------------
# Packaging stuff

RPM_RUNTIME_DEPS=""
AC_SUBST(RPM_RUNTIME_DEPS)

RPM_BUILD_DEPS=''
AC_SUBST(RPM_BUILD_DEPS)

DEB_RUNTIME_DEPS=""
AC_SUBST(DEB_RUNTIME_DEPS)

DEB_BUILD_DEPS=''
AC_SUBST(DEB_BUILD_DEPS)

PACKAGING_OS=`uname -s`
AC_SUBST(PACKAGING_OS)

RPM_BUILD_OPTS=''
AC_SUBST(RPMBUILD_OPTS)

ARCHITECTURE=`uname -i`
AC_SUBST(ARCHITECTURE)

#----------------------------------------------------------------------
# Checks for APR library
# uses the find_apr.m4 script to locate APR. sets apr_found and apr_config

sinclude(build/find_apr.m4)

APR_FIND_APR(,,1,[0 1])
if test "$apr_found" = "no"; then
	AC_MSG_ERROR(APR could not be located. Please use the --with-apr option.)
fi

APR_INCLUDES="`$apr_config --includes`"
AC_SUBST(APR_INCLUDES)

APR_LIBS="`$apr_config --link-libtool --ldflags --libs`"
AC_SUBST(APR_LIBS)

APR_SO_EXT="`$apr_config --apr-so-ext`"
AC_SUBST(APR_SO_EXT)

APR_LIB_TARGET="`$apr_config --apr-lib-target`"
AC_SUBST(APR_LIB_TARGET)

APR_CFLAGS="`$apr_config --cflags`"
AC_SUBST(APR_CFLAGS)

APR_CPPFLAGS="`$apr_config --cppflags`"
AC_SUBST(APR_CPPFLAGS)

APR_LDFLAGS="`$apr_config --ldflags`"
AC_SUBST(APR_LDFLAGS)

RPM_RUNTIME_DEPS="$RPM_RUNTIME_DEPS apr"
RPM_BUILD_DEPS="$RPM_BUILD_DEPS apr-devel"

DEB_RUNTIME_DEPS="$DEB_RUNTIME_DEPS libapr1"
DEB_BUILD_DEPS="$DEB_BUILD_DEPS libapr1-dev"

#----------------------------------------------------------------------
# Checks for compression libraries

sinclude(build/find_zlib.m4)
sinclude(build/find_bz2.m4)

#----------------------------------------------------------------------
# Checks for library functions.

# Don't use AC_FUNC_MALLOC nor AC_FUNC_REALLOC because we don't care about
# so-called 'GNU compatibility' of these functions

AC_CHECK_FUNCS([malloc realloc strchr memset free symlink memcpy strlen getppid getpid strncpy strcat sscanf link strcmp])

AC_CONFIG_FILES([Makefile
				build/managelogs.spec
				build/managelogs.dsc
				build/debian.changelog
				build/debian.control
				lib/Makefile
				src/Makefile
				doc/Makefile
				check/Makefile
				check/rcmd.sh])

AC_OUTPUT
