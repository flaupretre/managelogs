#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(managelogs, 1.2.0, managelogs-bugs@tekwire.net)

AC_CONFIG_SRCDIR([src/managelogs.c])
AM_CONFIG_HEADER(config.h)
AC_CONFIG_AUX_DIR(build)

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE
AM_AUX_DIR_EXPAND
AM_PROG_INSTALL_SH

sinclude(build/find_apr.m4)

# Checks for programs.

dnl Make sure we have an ANSI compiler
AM_C_PROTOTYPES
test "x$U" != "x" && AC_MSG_ERROR(Compiler not ANSI compliant)

AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

#AC_PROG_CC
AC_PROG_INSTALL
#AC_PROG_LIBTOOL

# Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h strings.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_SIZE_T

# Checks for libraries.

#----------------------------------------------------------------------
# Checks for APR library
# uses the find_apr.m4 script to locate APR. sets apr_found and apr_config

APR_FIND_APR(,,1,[0 1])
if test "$apr_found" = "no"; then
	AC_MSG_ERROR(APR could not be located. Please use the --with-apr option.)
fi

APR_INCLUDES="`$apr_config --includes`"
APR_LIBS="`$apr_config --link-libtool --libs`"
APR_SO_EXT="`$apr_config --apr-so-ext`"
APR_LIB_TARGET="`$apr_config --apr-lib-target`"
APR_CFLAGS="`$apr_config --cflags`"
APR_CPPFLAGS="`$apr_config --cppflags`"

AC_SUBST(APR_INCLUDES)
AC_SUBST(APR_LIBS)
AC_SUBST(APR_LIB_TARGET)
AC_SUBST(APR_CFLAGS)
AC_SUBST(APR_CPPFLAGS)

#----------------------------------------------------------------------
# Checks for compression libraries

sinclude(build/find_zlib.m4)
sinclude(build/find_bz2.m4)

#----------------------------------------------------------------------
# Checks for library functions.

AC_FUNC_FORK

AC_CHECK_FUNCS([atexit memset strchr])

AC_CONFIG_FILES([Makefile
				lib/Makefile
				src/Makefile
				doc/Makefile])

AC_OUTPUT
